::###############################################################################################
:: Copyright (C) 2024  Griefed
::
:: This script is free software; you can redistribute it and/or
:: modify it under the terms of the GNU Lesser General Public
:: License as published by the Free Software Foundation; either
:: version 2.1 of the License, or (at your option) any later version.
::
:: This library is distributed in the hope that it will be useful,
:: but WITHOUT ANY WARRANTY; without even the implied warranty of
:: MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
:: Lesser General Public License for more details.
::
:: You should have received a copy of the GNU Lesser General Public
:: License along with this library; if not, write to the Free Software
:: Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301
:: USA
::
:: The full license can be found at https:github.com/Griefed/ServerPackCreator/blob/main/LICENSE
::##############################################################################################
::
:: Start script generated by ServerPackCreator SPC_SERVERPACKCREATOR_VERSION_SPC.
:: The template which was used in the generation of this script can be found at:
::   https://github.com/Griefed/ServerPackCreator/blob/SPC_SERVERPACKCREATOR_VERSION_SPC/serverpackcreator-api/src/jvmMain/resources/de/griefed/resources/server_files/default_template.bat
::
:: Depending on which modloader is set, different checks are run to ensure the server will start accordingly.
:: If the modloader checks and setup are passed, Minecraft and EULA checks are run.
:: If everything is in order, the server is started.
::
:: Depending on the Minecraft version you will require a different Java version to run the server.
::   1.16.5 and older requires Java 8 (Java 11 will run better and work with 99% of mods, give it a try)
::     Linux:
::       You may acquire a Java 8 JRE here: https://adoptium.net/temurin/releases/?variant=openjdk8&version=8&package=jre&arch=x64&os=linux
::       You may acquire a java 11 JRE here: https://adoptium.net/temurin/releases/?variant=openjdk11&version=11&package=jre&arch=x64&os=linux
::     macOS:
::       You may acquire a Java 8 JRE here: https://adoptium.net/temurin/releases/?variant=openjdk8&version=8&package=jre&arch=x64&os=mac
::       You may acquire a java 11 JRE here: https://adoptium.net/temurin/releases/?variant=openjdk11&version=11&package=jre&arch=x64&os=mac
::   1.18.2 and newer requires Java 17 (Java 18 will run better and work with 99% of mods, give it a try)
::     Linux:
::       You may acquire a Java 17 JRE here: https://adoptium.net/temurin/releases/?variant=openjdk17&version=17&package=jre&arch=x64&os=linux
::       You may acquire a Java 18 JRE here: https://adoptium.net/temurin/releases/?variant=openjdk18&version=18&package=jre&arch=x64&os=linux
::     macOS:
::       You may acquire a Java 17 JRE here: https://adoptium.net/temurin/releases/?variant=openjdk17&version=17&package=jre&arch=x64&os=mac
::       You may acquire a Java 18 JRE here: https://adoptium.net/temurin/releases/?variant=openjdk18&version=18&package=jre&arch=x64&os=mac
@ECHO OFF
SETLOCAL EnableExtensions
SETLOCAL EnableDelayedExpansion
NET session >nul 2>&1

PUSHD %~dp0

IF %ERRORLEVEL%==0 (
    ECHO Warning^^! Running with administrator-privileges is not recommended.
)

ECHO Start script generated by ServerPackCreator SPC_SERVERPACKCREATOR_VERSION_SPC.
ECHO To change the launch settings of this server^, such as JVM args / flags^, Minecraft version^, modloader version etc.^, edit the variables.txt-file.

IF NOT EXIST variables.txt (
    ECHO ERROR^^! variables.txt not present. Without it the server can not be installed, configured or started.
    EXIT
)

:: Acquire variables from variables.txt
FOR /F "delims== tokens=1,2" %%G IN (variables.txt) DO (
	SET LINE=%%G
	IF NOT "%LINE:~0,1%"=="#" (
		SET %%G=%%H
	)
)

SET JAVA_ARGS=%JAVA_ARGS:~1,-1%
SET BACKSLASH=\
SET COLON=:
CALL SET JAVA=%%JAVA:\\=%BACKSLASH%%%
CALL SET JAVA=%%JAVA:\:=%COLON%%%

:: Variables with do_not_manually_edit are set automatically during script execution,
:: so manually editing them will have no effect, as they will be overridden.
SET MINECRAFTSERVERJARLOCATION=do_not_manually_edit
SET LAUNCHERJARLOCATION=do_not_manually_edit
SET SERVERRUNCOMANND=do_not_manually_edit

FOR /F "tokens=1,2,3 delims=." %%a IN ("%MINECRAFT_VERSION%") do (
	SET /A MC_MAJOR=%%a
	SET /A MC_MINOR=%%b
	SET /A MC_PATCH=%%c
)

GOTO :MAIN

:DELETEFILESILENTLY <FILE_TO_DELETE>
DEL /S /F /Q %~1
EXIT /B 0

:QUITSERVER
ECHO Exiting...
IF %WAIT_FOR_USER_INPUT%==true (
    PAUSE
)
EXIT

:CHECKJAVABITNESS
SET "OLD=0"
for /f "delims=" %%g in ('%JAVA% -version 2^>^&1') do (
	ECHO.%%~g| FIND /I "32-Bit">Nul && ( 
	  SET OLD=1
	)
)
IF %OLD%==1 (
	ECHO WARNING^^! 32-Bit Java detected^^! It is highly recommended to use a 64-Bit version of Java^^!
)
EXIT /B 0

:RUNJAVACOMMAND <COMMAND>
ECHO Running %~1
%JAVA% %~1
EXIT /B 0

:: FILETOCHECK = Filename to check for
:: FILETODOWNLOAD = Filename to save download as
:: DOWNLOADURL = URL to download $2 from
:DOWNLOADIFNOTEXISTS <FILETOCHECK> <FILETODOWNLOAD> <DOWNLOADURL>
IF NOT EXIST "%~1" (
	ECHO %~1 could not be found.
	ECHO Downloading %~2.
	ECHO from %~3.
	powershell.exe -noninteractive -command "Invoke-WebRequest %~3 -OutFile %~2"
	IF EXIST %~2 (
		EXIT /B 0
	) ELSE (
		EXIT /B 2
	)
) ELSE (
    ECHO %~1 present.
	EXIT /B 2
)

:: If modloader = Forge, run Forge-specific checks
:SETUPFORGE
ECHO.
ECHO Running Forge checks and setup...
SET FORGEINSTALLERURL=https://files.minecraftforge.net/maven/net/minecraftforge/forge/%MINECRAFT_VERSION%-%MODLOADER_VERSION%/forge-%MINECRAFT_VERSION%-%MODLOADER_VERSION%-installer.jar
SET FORGEJARLOCATION=do_not_manually_edit

IF %MC_MINOR% LEQ 16 (
	SET FORGEJARLOCATION=forge.jar
	SET LAUNCHERJARLOCATION=forge.jar
	SET MINECRAFTSERVERJARLOCATION=minecraft_server.%MINECRAFT_VERSION%.jar
	SET SERVERRUNCOMANND=%JAVA_ARGS% -jar !LAUNCHERJARLOCATION! nogui
) ELSE (
	SET FORGEJARLOCATION=libraries/net/minecraftforge/forge/%MINECRAFT_VERSION%-%MODLOADER_VERSION%/forge-%MINECRAFT_VERSION%-%MODLOADER_VERSION%-server.jar
	SET MINECRAFTSERVERJARLOCATION=libraries/net/minecraft/server/%MINECRAFT_VERSION%/server-%MINECRAFT_VERSION%.jar
	SET SERVERRUNCOMANND=@user_jvm_args.txt @libraries/net/minecraftforge/forge/%MINECRAFT_VERSION%-%MODLOADER_VERSION%/win_args.txt nogui

	ECHO Generating user_jvm_args.txt from variables...
	ECHO Edit JAVA_ARGS in your variables.txt. Do not edit user_jvm_args.txt directly^^!
	ECHO Manually made changes to user_jvm_args.txt will be lost in the nether^^!

	CALL:DELETEFILESILENTLY user_jvm_args.txt
	(
		ECHO # Xmx and Xms set the maximum and minimum RAM usage^, respectively.
		ECHO # They can take any number^, followed by an M or a G.
		ECHO # M means Megabyte^, G means Gigabyte.
		ECHO # For example^, to set the maximum to 3GB: -Xmx3G
		ECHO # To set the minimum to 2.5GB: -Xms2500M
		ECHO # A good default for a modded server is 4GB.
		ECHO # Uncomment the next line to set it.
		ECHO # -Xmx4G
		ECHO %JAVA_ARGS%
	)> "user_jvm_args.txt"
)

CALL:DOWNLOADIFNOTEXISTS !FORGEJARLOCATION! forge-installer.jar !FORGEINSTALLERURL!
IF ERRORLEVEL 0 (

	ECHO Forge Installer downloaded. Installing...
	CALL:RUNJAVACOMMAND "-jar forge-installer.jar --installServer"

	IF %MC_MINOR% GTR 16 (
		CALL:DELETEFILESILENTLY run.bat
		CALL:DELETEFILESILENTLY run.sh
	) ELSE (
		ECHO Renaming forge-%MINECRAFT_VERSION%-%MODLOADER_VERSION%.jar to forge.jar
		REN forge-%MINECRAFT_VERSION%-%MODLOADER_VERSION%.jar forge.jar
		REN forge-%MINECRAFT_VERSION%-%MODLOADER_VERSION%-universal.jar forge.jar
	)

	IF EXIST !FORGEJARLOCATION! (
		CALL:DELETEFILESILENTLY forge-installer.jar
		ECHO Installation complete. forge-installer.jar deleted.
	) ELSE (
		CALL:DELETEFILESILENTLY forge-installer.jar
		ECHO Something went wrong during the server installation. Please try again in a couple of minutes and check your internet connection.
		CALL:QUITSERVER
	)
)
EXIT /B 0


:: If modloader = NeoForge, run NeoForge-specific checks
:SETUPNEOFORGE
ECHO.
ECHO Running NeoForge checks and setup...
SET FORGEJARLOCATION=do_not_manually_edit
SET JARFOLDER=do_not_manually_edit

IF %MC_MINOR% EQU 20 (
	IF %MC_PATCH% GTR 1 (
		SET JARFOLDER=libraries/net/neoforged/neoforge/%MODLOADER_VERSION%
		SET FORGEJARLOCATION=!JARFOLDER!/neoforge-%MODLOADER_VERSION%-server.jar
	) ELSE (
		SET JARFOLDER=libraries/net/neoforged/forge/%MINECRAFT_VERSION%-%MODLOADER_VERSION%
		SET FORGEJARLOCATION=!JARFOLDER!/forge-%MINECRAFT_VERSION%-%MODLOADER_VERSION%-server.jar
	)
) ELSE (
	SET JARFOLDER=libraries/net/neoforged/neoforge/%MODLOADER_VERSION%
	SET FORGEJARLOCATION=!JARFOLDER!/neoforge-%MODLOADER_VERSION%-server.jar
)

SET MINECRAFTSERVERJARLOCATION=libraries/net/minecraft/server/%MINECRAFT_VERSION%/server-%MINECRAFT_VERSION%.jar
SET SERVERRUNCOMANND=@user_jvm_args.txt @!JARFOLDER!/win_args.txt nogui

ECHO Generating user_jvm_args.txt from variables...
ECHO Edit JAVA_ARGS in your variables.txt. Do not edit user_jvm_args.txt directly^^!
ECHO Manually made changes to user_jvm_args.txt will be lost in the nether^^!

CALL:DELETEFILESILENTLY user_jvm_args.txt

(
	ECHO # Xmx and Xms set the maximum and minimum RAM usage^, respectively.
	ECHO # They can take any number^, followed by an M or a G.
	ECHO # M means Megabyte^, G means Gigabyte.
	ECHO # For example^, to set the maximum to 3GB: -Xmx3G
	ECHO # To set the minimum to 2.5GB: -Xms2500M
	ECHO # A good default for a modded server is 4GB.
	ECHO # Uncomment the next line to set it.
	ECHO # -Xmx4G
	ECHO %JAVA_ARGS%
)> "user_jvm_args.txt"

CALL:DOWNLOADIFNOTEXISTS !FORGEJARLOCATION! neoforge-installer.jar %NEOFORGE_INSTALLER_URL%
IF ERRORLEVEL 0 (
	ECHO NeoForge Installer downloaded. Installing...
	CALL:RUNJAVACOMMAND "-jar neoforge-installer.jar --installServer"
	ECHO Renaming forge-%MINECRAFT_VERSION%-%MODLOADER_VERSION%.jar to forge.jar
	REN forge-%MINECRAFT_VERSION%-%MODLOADER_VERSION%.jar forge.jar

	IF EXIST !FORGEJARLOCATION! (
		CALL:DELETEFILESILENTLY neoforge-installer.jar
		CALL:DELETEFILESILENTLY neoforge-installer.jar.log
		ECHO Installation complete. forge-installer.jar deleted.
	) ELSE (
		CALL:DELETEFILESILENTLY neoforge-installer.jar
		ECHO Something went wrong during the server installation. Please try again in a couple of minutes and check your internet connection.
		CALL:QUITSERVER
	)
)
EXIT /B 0

:: If modloader = Fabric, run Fabric-specific checks
:SETUPFABRIC
ECHO.
ECHO Running Fabric checks and setup...
SET FABRICINSTALLERURL=https://maven.fabricmc.net/net/fabricmc/fabric-installer/%FABRIC_INSTALLER_VERSION%/fabric-installer-%FABRIC_INSTALLER_VERSION%.jar
SET IMPROVEDFABRICLAUNCHERURL=https://meta.fabricmc.net/v2/versions/loader/%MINECRAFT_VERSION%/%MODLOADER_VERSION%/%FABRIC_INSTALLER_VERSION%/server/jar
SET FABRICURL=https://meta.fabricmc.net/v2/versions/loader/%MINECRAFT_VERSION%/%MODLOADER_VERSION%/server/json
SET IMPROVEDFABRICLAUNCHERAVAILABLE=
SET FABRICAVAILABLE=
FOR /F %%a in ( 'curl -k -s -o NUL -w "%%{http_code}" "%IMPROVEDFABRICLAUNCHERURL%"' ) DO SET IMPROVEDFABRICLAUNCHERAVAILABLE=%%a
FOR /F %%b in ( 'curl -k -s -o NUL -w "%%{http_code}" "%FABRICURL%"' ) DO SET FABRICAVAILABLE=%%b

IF %IMPROVEDFABRICLAUNCHERAVAILABLE%==200 (
	ECHO Improved Fabric Server Launcher available...
	ECHO The improved launcher will be used to run this Fabric server.
	SET LAUNCHERJARLOCATION=fabric-server-launcher.jar
	CALL:DOWNLOADIFNOTEXISTS !LAUNCHERJARLOCATION! !LAUNCHERJARLOCATION! %IMPROVEDFABRICLAUNCHERURL%
) ELSE (

	IF %FABRICAVAILABLE%==200 (
		ECHO Fabric available. Continuing.
	) ELSE (
		ECHO Fabric is not available for Minecraft %MINECRAFT_VERSION%^, Fabric %MODLOADER_VERSION%.
		CALL:QUITSERVER
	)

	CALL:DOWNLOADIFNOTEXISTS fabric-server-launch.jar fabric-installer.jar %FABRICINSTALLERURL%

	IF ERRORLEVEL 0 (
	
		ECHO Installer downloaded...
		SET LAUNCHERJARLOCATION=fabric-server-launch.jar
		SET MINECRAFTSERVERJARLOCATION=server.jar
		CALL:RUNJAVACOMMAND "-jar fabric-installer.jar server -mcversion %MINECRAFT_VERSION% -loader %MODLOADER_VERSION% -downloadMinecraft"

		IF EXIST "fabric-server-launch.jar" (
			CALL:DELETEFILESILENTLY .fabric-installer
			CALL:DELETEFILESILENTLY fabric-installer.jar
			ECHO Installation complete. fabric-installer.jar deleted.
		) ELSE (
			CALL:DELETEFILESILENTLY fabric-installer.jar
			ECHO fabric-server-launch.jar not found. Maybe the Fabric servers are having trouble.
			ECHO Please try again in a couple of minutes and check your internet connection.
			CALL:QUITSERVER
		)
	) ELSE (
		SET LAUNCHERJARLOCATION=fabric-server-launcher.jar
		SET MINECRAFTSERVERJARLOCATION=server.jar
	)
)
SET "SERVERRUNCOMANND=!JAVA_ARGS! -jar !LAUNCHERJARLOCATION! nogui"
ECHO "%SERVERRUNCOMANND%"
EXIT /B 0


:: If modloader = Quilt, run Quilt-specific checks
:SETUPQUILT
ECHO.
ECHO Running Quilt checks and setup...
SET QUILTINSTALLERURL=https://maven.quiltmc.org/repository/release/org/quiltmc/quilt-installer/%QUILT_INSTALLER_VERSION%/quilt-installer-%QUILT_INSTALLER_VERSION%.jar
SET CHECKURL=https://meta.fabricmc.net/v2/versions/intermediary/%MINECRAFT_VERSION%
SET COUNT=
FOR /F %%i IN ('powershell.exe -noninteractive -command ^(ConvertFrom-JSON ^(Invoke-WebRequest -Uri "%CHECKURL%"^)^).Length') DO (
    SET /A COUNT=%%i
)

IF %COUNT% EQU 0 (
	ECHO Quilt is not available for Minecraft %MINECRAFT_VERSION%^, Quilt %MODLOADER_VERSION%.
	CALL:QUITSERVER
) ELSE (
	CALL:DOWNLOADIFNOTEXISTS quilt-server-launch.jar quilt-installer.jar %QUILTINSTALLERURL%
	IF EXIST quilt-installer.jar (
	
		ECHO Installer downloaded. Installing...
		CALL:RUNJAVACOMMAND "-jar quilt-installer.jar install server %MINECRAFT_VERSION% --download-server --install-dir=."

		IF EXIST quilt-server-launch.jar (
			CALL:DELETEFILESILENTLY quilt-installer.jar
			ECHO Installation complete. quilt-installer.jar deleted.
		) ELSE (
			CALL:DELETEFILESILENTLY quilt-installer.jar
			ECHO quilt-server-launch.jar not found. Maybe the Quilt servers are having trouble.
			ECHO Please try again in a couple of minutes and check your internet connection.
			CALL:QUITSERVER
		)
	)
)
SET LAUNCHERJARLOCATION=quilt-server-launch.jar
SET MINECRAFTSERVERJARLOCATION=server.jar
SET SERVERRUNCOMANND=%JAVA_ARGS% -jar !LAUNCHERJARLOCATION! nogui
EXIT /B 0

:: If modloader = LegacyFabric, run LegacyFabric-specific checks
:SETUPLEGACYFABRIC
ECHO.
ECHO Running LegacyFabric checks and setup...
SET LEGACYFABRICINSTALLERURL=https://maven.legacyfabric.net/net/legacyfabric/fabric-installer/%LEGACYFABRIC_INSTALLER_VERSION%/fabric-installer-%LEGACYFABRIC_INSTALLER_VERSION%.jar
SET CHECKURL=https://meta.legacyfabric.net/v2/versions/loader/%MINECRAFT_VERSION%
SET COUNT=
FOR /F %%i IN ('powershell.exe -noninteractive -command ^(ConvertFrom-JSON ^(Invoke-WebRequest -Uri "%CHECKURL%"^)^).Length') DO (
    SET /A COUNT=%%i
)

IF %COUNT% EQU 0 (
	ECHO LegacyFabric is not available for Minecraft %MINECRAFT_VERSION%^, LegacyFabric %MODLOADER_VERSION%.
	CALL:QUITSERVER
) ELSE (
	CALL:DOWNLOADIFNOTEXISTS fabric-server-launch.jar legacyfabric-installer.jar %LEGACYFABRICINSTALLERURL%
	IF EXIST legacyfabric-installer.jar (

		ECHO Installer downloaded. Installing...
		CALL:RUNJAVACOMMAND "-jar legacyfabric-installer.jar server -mcversion %MINECRAFT_VERSION% -loader %MODLOADER_VERSION% -downloadMinecraft"

		IF EXIST fabric-server-launch.jar (

			CALL:DELETEFILESILENTLY legacyfabric-installer.jar
			ECHO Installation complete. legacyfabric-installer.jar deleted.

		) ELSE (

			CALL:DELETEFILESILENTLY legacyfabric-installer.jar
			ECHO fabric-server-launch.jar not found. Maybe the LegacyFabric servers are having trouble.
			ECHO Please try again in a couple of minutes and check your internet connection.
			CALL:QUITSERVER
		)
	)
)

SET LAUNCHERJARLOCATION=fabric-server-launch.jar
SET MINECRAFTSERVERJARLOCATION=server.jar
SET SERVERRUNCOMANND=%JAVA_ARGS% -jar !LAUNCHERJARLOCATION! nogui
EXIT /B 0

:: Check for a minecraft server and download it if necessary
:MINECRAFT
IF %MODLOADER%==Fabric (
	IF !IMPROVEDFABRICLAUNCHERAVAILABLE!==200 (
		ECHO Skipping Minecraft Server JAR checks because we are using the improved Fabric Server Launcher.
		GOTO :MC_DONE
	) ELSE (
		GOTO :MC_REGULAR
	)
) ELSE (
	GOTO :MC_REGULAR
)
:MC_REGULAR
CALL:DOWNLOADIFNOTEXISTS !MINECRAFTSERVERJARLOCATION! !MINECRAFTSERVERJARLOCATION! %MINECRAFT_SERVER_URL%
:MC_DONE
EXIT /B 0

:: Check for eula.txt and generate if necessary
:EULA
IF NOT EXIST eula.txt (
	ECHO Mojang's EULA has not yet been accepted. In order to run a Minecraft server^, you must accept Mojang's EULA.
	ECHO Mojang's EULA is available to read at https://aka.ms/MinecraftEULA
	ECHO If you agree to Mojang's EULA then type 'I agree'
	SET "EXPECT=I agree"
	SET /P "ANSWER=Answer: "

	IF !ANSWER!==!EXPECT! (
	
		ECHO User agreed to Mojang's EULA.
		(
			ECHO #By changing the setting below to TRUE you are indicating your agreement to our EULA ^(https://aka.ms/MinecraftEULA^).
			ECHO eula^=true
		)> "eula.txt"
		
	) ELSE (
		ECHO User did not agree to Mojang's EULA.
		ECHO Entered: %ANSWER%
		ECHO You can not run a Minecraft server unless you agree to Mojang's EULA.
		CALL:QUITSERVER
	)
)
EXIT /B 0

:MAIN
:MODLOADER-SWITCH
	GOTO MODLOADER-SWITCH-%MODLOADER% 2>NUL || (
		ECHO Incorrect modloader specified: %MODLOADER%
        CALL:QUITSERVER
	)
	
	:MODLOADER-SWITCH-Forge
		CALL:SETUPFORGE
		GOTO :MODLOADER-SWITCH-END
	
	:MODLOADER-SWITCH-NeoForge
		CALL:SETUPNEOFORGE
		GOTO :MODLOADER-SWITCH-END
	
	:MODLOADER-SWITCH-Fabric
		CALL:SETUPFABRIC
		GOTO :MODLOADER-SWITCH-END
	
	:MODLOADER-SWITCH-Quilt
		CALL:SETUPQUILT
		GOTO :MODLOADER-SWITCH-END
	
	:MODLOADER-SWITCH-LegacyFabric
		CALL:SETUPLEGACYFABRIC
		GOTO :MODLOADER-SWITCH-END
	
:MODLOADER-SWITCH-END

CALL:CHECKJAVABITNESS
CALL:MINECRAFT
CALL:EULA

ECHO.
ECHO Starting server...
ECHO Minecraft version:              %MINECRAFT_VERSION%
ECHO Modloader:                      %MODLOADER%
ECHO Modloader version:              %MODLOADER_VERSION%
ECHO LegacyFabric Installer Version: %LEGACYFABRIC_INSTALLER_VERSION%
ECHO Fabric Installer Version:       %FABRIC_INSTALLER_VERSION%
ECHO Quilt Installer Version:        %QUILT_INSTALLER_VERSION%
ECHO NeoForge Installer URL:         %NEOFORGE_INSTALLER_URL%
ECHO Minecraft Server URL:           %MINECRAFT_SERVER_URL%
ECHO Java Args:                      %JAVA_ARGS%
ECHO Additional Args:                %ADDITIONAL_ARGS%
ECHO Java Path:                      %JAVA%
ECHO Wait For User Input:            %WAIT_FOR_USER_INPUT%
IF NOT !LAUNCHERJARLOCATION!=="do_not_manually_edit" (
    ECHO Launcher JAR:                   !LAUNCHERJARLOCATION!
)
ECHO Run Command:       %JAVA% %ADDITIONAL_ARGS% !SERVERRUNCOMANND!
ECHO Java version:
CALL:RUNJAVACOMMAND -version
ECHO.

CALL:RUNJAVACOMMAND "%ADDITIONAL_ARGS% !SERVERRUNCOMANND!"

ECHO.
CALL:QUITSERVER
ECHO.