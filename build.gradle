import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
    id 'edu.sc.seis.launch4j' version '2.5.0'
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

group 'de.griefed'

sourceSets {
    //noinspection GroovyAssignabilityCheck
    main {
        java {
            java.srcDirs = ['src/main/java']
        }
    }
}

launch4j {
    fileDescription = "An application to create a server pack from any given Forge or Fabric Minecraft modpack."
    version = System.getenv("NEW_VERSION").toString()
    textVersion = System.getenv("NEW_VERSION").toString()
    windowTitle = "serverpackcreator"
    copyright = "GNU Lesser General Public License v2.1"
    downloadUrl = "https://github.com/Griefed/serverpackcreator/releases"
    supportUrl = "https://github.com/Griefed/serverpackcreator/issues"
    libraryDir = 'libraries'
    outputDir = 'libs'
    mainClassName = 'de.griefed.serverpackcreator.Main'
    icon = "${projectDir}/src/main/resources/de/griefed/resources/gui/icon.ico"
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    embed 'org.slf4j:slf4j-log4j12:2.0.0-alpha3'
    embed 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    embed 'com.typesafe:config:1.4.1'
    embed 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    embed 'commons-io:commons-io:2.11.0'
    embed 'net.lingala.zip4j:zip4j:2.9.0'
    embed 'com.github.FabricMC:fabric-installer:master-SNAPSHOT'
    embed 'com.github.TheRandomLabs:CurseAPI:master-SNAPSHOT'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed",
                "skipped",
                "failed"
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PRIVATE
    classpath = sourceSets.main.runtimeClasspath
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.embed.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    //noinspection GroovyAssignabilityCheck
    manifest {
        attributes(
                "Main-Class"     : "de.griefed.serverpackcreator.Main",
                "Class-Path"     : configurations.embed.findAll { it.name.endsWith('jar') }.collect { zipTree(it) },
                "Description"    : "Create server packs from Minecraft modpacks.",
                "Built-By"       : System.getProperty("user.name"),
                "Build-Timestamp": new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Created-By"     : "Gradle ${gradle.gradleVersion}",
                "Build-Jdk"      : "${System.getProperty('java.version')} (${System.getProperty('java.vendor')} ${System.getProperty('java.vm.version')})",
                "Build-OS"       : "${System.getProperty('os.name')} ${System.getProperty('os.arch')} ${System.getProperty('os.version')}",
        )
    }
    exclude (
            'META-INF/org',
            'META-INF/org/**',
            'META-INF/versions',
            'META-INF/versions/**',
            'META-INF/DEPENDENCIES',
            'META-INF/LICENSE',
            'META-INF/NOTICE',
            'META-INF/CHANGES',
            'META-INF/LICENSE.txt',
            'META-INF/NOTICE.txt',
            'META-INF/README.md',
            'Log4j-**',
            'icon.png',
            'profile_icon.png'
    )
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Griefed/serverpackcreator")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        //noinspection GroovyAssignabilityCheck
        gpr(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            artifactId='serverpackcreator'
            //noinspection GroovyAssignabilityCheck
            from (components.java)
        }
    }
}